import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'https://maven.minecraftforge.net'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    // This is only here while FG is under active development
    configurations.classpath {
        resolutionStrategy {
            cacheDynamicVersionsFor 10, 'seconds'
            cacheChangingModulesFor 0, 'seconds'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
        classpath 'org.jetbrains.kotlin:kotlin-serialization:1.6.10'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'

//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
idea {
    module {
        //Exclude directories from being managed
        for (String excludeDirName in ["run", "out", "logs", "gradle"]) {
            File excludeDir = new File(projectDir, excludeDirName)
            excludeDirs.add(excludeDir)
        }
    }
}

group = 'com.tencao'
version = mc_version + '-' + mod_version
archivesBaseName = "saoui"

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

sourceSets.main.resources { srcDir 'src/generated/resources' }



minecraft {
    // The mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   Snapshot are built nightly.
    // stable_#            Stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    //mappings channel: 'official', version: '1.16.5'
    mappings channel: 'snapshot', version: '20210309-1.16.5'

    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                source sourceSets.main
            }
        }

        server {
            workingDirectory file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            mods {
                source sourceSets.main
            }
        }

        data {
            workingDirectory file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'com.tencao.saoui', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                source sourceSets.main
            }
        }
    }
}

def replaceResources = tasks.register("replaceResources", Copy) {
    it.outputs.upToDateWhen { false }
    //Copy it into the build dir
    it.from(sourceSets.main.resources) {
        include "META-INF/mods.toml"
        expand 'version': mod_version, 'mc_version': minecraft_version_range, 'forge_version': forge_version_range
    }
    it.into "$buildDir/resources/main/"
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.FAIL)
    exclude('META-INF/mods.toml')
    configure { finalizedBy(replaceResources) }
}

classes.configure {
    dependsOn(replaceResources)
}

// This is only here while FG is under active development
configurations.all  {
    resolutionStrategy {
        cacheDynamicVersionsFor 10, 'seconds'
        cacheChangingModulesFor 0, 'seconds'
    }
}

configurations {
    shadow  {
        transitive = false
    }
    implementation.extendsFrom shadow
    apiImplementation.extendsFrom(implementation)
    apiRuntimeOnly.extendsFrom(runtimeOnly)
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
    maven {
        url "https://maven.bluexin.be/repository/releases/"
    }
    maven {
        url "https://maven.bluexin.be/repository/snapshots/"
    }
    maven {
        name = 'kotlinforforge'
        url = 'https://thedarkcolour.github.io/KotlinForForge/'
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.34'
    implementation 'thedarkcolour:kotlinforforge:1.17.0'
    implementation 'com.ibm.icu:icu4j:70.1'
    shadow 'be.bluexin.gnu.jel:gnu-jel:2.1.3:debug'
    shadow 'com.helger:ph-css:6.5.0'
    shadow 'com.helger.commons:ph-commons:10.1.6'
    shadow 'org.slf4j:slf4j-api:1.7.36'

    implementation fg.deobf('com.tencao:saomclib:1.16.5+') {changing = true}
    implementation group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
    //implementation ('com.tencao:saomclib:1.16.5+') {changing = true}
    //implementation fg.deobf(
           // project.dependencies.create('com.tencao:saomclib:1.16.5-1.4.9')
    //)
    //implementation fg.deobf('saomclib-1.16.5-1.4.9-sources')
    //implementation fg.deobf(fileTree(dir: 'libs', include: ['*.jar']))
}

jar {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    for (dep in configurations.shadow) {
        from(project.zipTree(dep)) {
            exclude 'META-INF/MANIFEST.MF', 'META-INF/buildinfo.xml', 'LICENSE*', 'changelog*', 'NOTICE*',
                    'javax/annotation/**', 'edu/umd/cs/findbugs/annotations/**'
        }
    }


    manifest.attributes([
            "Specification-Title"     : "saoui",
            "Specification-Vendor"    : "Tencao, Bluexin",
            "Specification-Version"   : "1", // We are version 1 of ourselves
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : project.version,
            "Implementation-Vendor"   : "Tencao, Bluexin",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
    ])

    finalizedBy 'reobfJar'
}

tasks.withType(Copy) {
    includeEmptyDirs = false
    duplicatesStrategy = DuplicatesStrategy.WARN
}

task sourcesJar(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task deobfJar(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    archiveClassifier.set('deobf')
    from sourceSets.main.output
}

task shadeJar(type: ShadowJar) {
    duplicatesStrategy(DuplicatesStrategy.INCLUDE)
    from sourceSets.main.output
    configurations = [project.configurations.shadow]

    //relocate 'gnu.jel', 'saoui.shade.gnu.jel'
    relocate 'com.helger', 'saoui.shade.com.helger'
    relocate 'org.slf4j', 'saoui.shade.org.slf4j'
    finalizedBy 'reobfJar'
}

artifacts {
    archives deobfJar
    archives sourcesJar
    archives shadeJar
}


java {
    withSourcesJar()
}

jar.finalizedBy('reobfJar')

tasks.build.dependsOn('shadeJar')
tasks.idea.dependsOn('shadeJar')
publish.dependsOn('reobfJar', 'deobfJar', 'sourcesJar', 'shadeJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
}

compileKotlin {
    kotlinOptions {
        javaParameters = true
        jvmTarget = 1.8
    }
}

tasks.withType(JavaCompile) {
    options.incremental = false
}